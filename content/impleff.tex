\section{Creating New Effects}

\label{sect:impleff}

We have now seen several side-effecting operations provided by the \effects{}
library, and examples of their use in Section \ref{sect:simpleff}. We have also
seen how operations may \emph{modify} the available effects by changing
state in Section \ref{sect:depeff}. We have not, however, yet seen how 
these operations are implemented. In this section, we describe how a selection
of the available effects are implemented, and show how new effectful operations
may be provided.

\subsection{State}

Effects are described by \emph{algebraic data types}, where the constructors
describe the operations provided when the effect is available. Stateful
operations are described as follows:

\begin{code}
data State : Effect where
     Get :      { a }       State a
     Put : b -> { a ==> b } State () 
\end{code}

\noindent
Each effect is associated with a \emph{resource}, the type of which is given
with the notation 
\texttt{\{ x ==> x' \}}. This notation gives the resource type expected by
each operation, and how it updates when the operation is run. Here, it means:

\begin{itemize}
\item \texttt{Get} takes no arguments. It has a resource of type \texttt{a},
which is not updated, and running the \texttt{Get} operation returns something
of type \texttt{a}.
\item \texttt{Put} takes a \texttt{b} as an argument. It has a resource of type
\texttt{a} on input, which is updated to a resource of type \texttt{b}.
Running the \texttt{Put} operation returns the element of the unit type.
\end{itemize}

\noindent
\texttt{Effect} itself is a type synonym, declared as follows:

\begin{code}
Effect : Type
Effect = (result : Type) -> 
         (input_resource : Type) -> 
         (output_resource : result -> Type) -> Type
\end{code}

\noindent
That is, an effectful operation returns something of type \texttt{result},
has an input resource of type \texttt{input\_resource}, and a function
\texttt{output\_resource} which computes the output resource type from
the result. We use the same syntactic sugar as with \texttt{Eff} to make
effect declarations more readable. It is defined as follows in
the \effects{} library:

\begin{code}
syntax "{" [inst] "}" [eff] = eff inst (\result => inst)
syntax "{" [inst] "==>" "{" {b} "}" [outst] "}" [eff] 
       = eff inst (\b => outst)
syntax "{" [inst] "==>" [outst] "}" [eff] = eff inst (\result => outst)
\end{code}

\noindent
In order to convert \texttt{State} (of type \texttt{Effect}) into something
usable in an effects list, of type \texttt{EFFECT}, we write the following:

\begin{code}
STATE : Type -> EFFECT
STATE t = MkEff t State
\end{code}

\noindent
\texttt{MkEff} constructs an \texttt{EFFECT} by taking the resource type
(here, the \texttt{t} which parameterises \texttt{STATE})
and the effect signature (here, \texttt{State}). For reference,
\texttt{EFFECT} is declared as follows:

\begin{code}
data EFFECT : Type where
     MkEff : Type -> Effect -> EFFECT
\end{code}

\noindent
Recall that to run an effectful program in \texttt{Eff}, we use one of the
\texttt{run} family of functions to run the program in a particular
computation context \texttt{m}. For each effect, therefore, we must explain
how it is executed in a particular computation context for \texttt{run} to
work in that context. This is achieved with the following type class:

\begin{code}
class Handler (e : Effect) (m : Type -> Type) where
      handle : resource -> (eff : e t resource resource') -> 
               ((x : t) -> resource' x -> m a) -> m a
\end{code}

\noindent
We have already seen some instance declarations 
in the effect summaries in Section \ref{sect:simpleff}. An instance of
\texttt{Handler e m} means that the effect declared with signature \texttt{e}
can be run in computation context \texttt{m}. The \texttt{handle} function
takes:

\begin{itemize}
\item The \texttt{resource} on input (so, the current value of the state for
\texttt{State})
\item The effectful operation (either \texttt{Get} or \texttt{Put x} for
\texttt{State})
\item A \emph{continuation}, which we conventionally call \texttt{k},
and should be passed the result value of the
operation, and an updated resource.
\end{itemize}

\noindent
There are two reasons for taking a continuation here: firstly, this is neater
because there are multiple return values (a new resource and the result of
the operation); secondly, and more importantly, the continuation can be called
zero or more times.

A \texttt{Handler} for \texttt{State} simply passes on the value of the state,
in the case of \texttt{Get}, or passes on a new state, in the case of \texttt{Put}.
It is defined the same way for all computation contexts:

\begin{code}
instance Handler State m where
     handle st Get     k = k st st
     handle st (Put n) k = k () n
\end{code}

\noindent
This gives enough information for \texttt{Get} and \texttt{Put} to be used
directly in \texttt{Eff} programs. It is tidy, however, to define top level
functions in \texttt{Eff}, as follows:

\begin{code}
get : { [STATE x] } Eff x
get = call Get

put : x -> { [STATE x] } Eff () 
put val = call (Put val)

putM : y -> { [STATE x] ==> [STATE y] } Eff () 
putM val = call (Put val)
\end{code}

\noindent
\textbf{An implementation detail (aside):} 
The \texttt{call} function converts an \texttt{Effect} to a function in
\texttt{Eff}, given a proof that the effect is
available. This proof can be constructed automatically by \Idris{}, since it
is essentially an index into a statically known list of effects:

\begin{code}
call : {e : Effect} ->
       (eff : e t a b) -> {auto prf : EffElem e a xs} ->
       Eff t xs (\v => updateResTy v xs prf eff)
\end{code}

\noindent
This is the reason for the \texttt{Can't solve goal} error when an effect is
not available: the implicit proof \texttt{prf} has not been solved automatically
because the required effect is not in the list of effects \texttt{xs}.

Such details are not important for using the library, or even writing new
effects, however. 

\subsubsection*{Summary}

Listing \ref{eff:statedef} summarises what is required to define the
\texttt{STATE} effect.

\begin{code}[float=t,frame=single, caption={Complete State Effect Definition}, label=eff:statedef]
data State : Effect where
     Get :      { a }       State a
     Put : b -> { a ==> b } State () 

STATE : Type -> EFFECT
STATE t = MkEff t State

instance Handler State m where
     handle st Get     k = k st st
     handle st (Put n) k = k () n

get : { [STATE x] } Eff x
get = call Get

put : x -> { [STATE x] } Eff () 
put val = call (Put val)

putM : y -> { [STATE x] ==> [STATE y] } Eff () 
putM val = call (Put val)
\end{code}

\subsection{Console I/O}

Listing \ref{eff:stdiodef} gives the definition of the
\texttt{STDIO} effect, including handlers for \texttt{IO} and
\texttt{IOExcept}. We omit the definition of the top level \texttt{Eff}
functions, as this merely invoke the effects \texttt{PutStr}, \texttt{GetStr},
\texttt{PutCh} and \texttt{GetCh} directly.

Note that in this case, the resource is the unit type in every case, since
the handlers merely apply the \texttt{IO} equivalents of the effects
directly.

\begin{code}[float=h,frame=single, caption={Console I/O Effect Definition}, label=eff:stdiodef]
data StdIO : Effect where
     PutStr : String -> { () } StdIO () 
     GetStr : { () } StdIO String 
     PutCh : Char -> { () } StdIO ()
     GetCh : { () } StdIO Char

instance Handler StdIO IO where
    handle () (PutStr s) k = do putStr s; k () ()
    handle () GetStr     k = do x <- getLine; k x ()
    handle () (PutCh c)  k = do putChar c; k () () 
    handle () GetCh      k = do x <- getChar; k x ()

instance Handler StdIO (IOExcept a) where
    handle () (PutStr s) k = do ioe_lift $ putStr s; k () ()
    handle () GetStr     k = do x <- ioe_lift $ getLine; k x ()
    handle () (PutCh c)  k = do ioe_lift $ putChar c; k () () 
    handle () GetCh      k = do x <- ioe_lift $ getChar; k x ()

STDIO : EFFECT
STDIO = MkEff () StdIO
\end{code}

\subsection{Exceptions}

\begin{code}[float=h,frame=single, caption={Exception Effect Definition}, label=eff:exceptdef]
data Exception : Type -> Effect where 
     Raise : a -> { () } Exception a b

instance Handler (Exception a) Maybe where
     handle _ (Raise e) k = Nothing

instance Handler (Exception a) List where
     handle _ (Raise e) k = []

EXCEPTION : Type -> EFFECT
EXCEPTION t = MkEff () (Exception t)
\end{code}

Listing \ref{eff:exceptdef} gives the definition of the \texttt{Exception}
effect, including two of its handlers for \texttt{Maybe} and \texttt{List}.
The only operation provided is \texttt{Raise}. 
%
The key point to note in the definitions of these handlers is that the
continuation \texttt{k} is not used. Running \texttt{Raise} therefore means
that computation stops with an error.

\subsection{Non-determinism}

\begin{code}[float=h,frame=single, caption={Non-determinism Effect Definition}, label=eff:selectdef]
data Selection : Effect where
     Select : List a -> { () } Selection a 

instance Handler Selection Maybe where
     handle _ (Select xs) k = tryAll xs where
         tryAll [] = Nothing
         tryAll (x :: xs) = case k x () of
                                 Nothing => tryAll xs
                                 Just v => Just v

instance Handler Selection List where
     handle r (Select xs) k = concatMap (\x => k x r) xs

SELECT : EFFECT
SELECT = MkEff () Selection
\end{code}

Listing \ref{eff:selectdef} gives the definition of the \texttt{Select} effect
for writing non-deterministic programs, including a handler for \texttt{List}
context which returns all possible successful values, and a handler for
\texttt{Maybe} context which returns the first successful value.

Here, the continuation is called multiple times in each handler, for each
value in the list of possible values. In the \texttt{List} handler, we
accumulate all successful results, and in the \texttt{Maybe} handler we
try the first value in the last, and try later values only if that fails.

\subsection{File Management}

Result-dependent effects are no different from non-dependent effects in
the way they are implemented. Listing \ref{eff:filedef} illustrates this
for the \texttt{FILE\_IO} effect. The syntax for state transitions 
\texttt{\{ x ==> \{res\} x' \}}, where the result state \texttt{x'} is
computed from the result of the operation \texttt{res}, 
follows that for the equivalent
\texttt{Eff} programs.

Note that in the handler for \texttt{Open}, the types passed to the
continuation \texttt{k} are different depending on whether the result
is \texttt{True} (opening succeeded) or \texttt{False} (opening failed).
This uses \texttt{validFile}, defined in the \texttt{Prelude}, to test
whether a file handler refers to an open file or not.

\begin{code}[float=h,frame=single, caption={File I/O Effect Definition}, label=eff:filedef]
data FileIO : Effect where
     Open  : String -> (m : Mode) -> 
             {() ==> {res} if res then OpenFile m else ()} FileIO Bool
     Close : {OpenFile m ==> ()}                           FileIO () 

     ReadLine  :           {OpenFile Read}  FileIO String 
     WriteLine : String -> {OpenFile Write} FileIO ()
     EOF       :           {OpenFile Read}  FileIO Bool

instance Handler FileIO IO where
    handle () (Open fname m) k = do h <- openFile fname m
                                    if !(validFile h)
                                             then k True (FH h) 
                                             else k False ()
    handle (FH h) Close      k = do closeFile h
                                    k () ()

    handle (FH h) ReadLine        k = do str <- fread h
                                         k str (FH h)
    handle (FH h) (WriteLine str) k = do fwrite h str
                                         k () (FH h)
    handle (FH h) EOF             k = do e <- feof h
                                         k e (FH h)

FILE_IO : Type -> EFFECT
FILE_IO t = MkEff t FileIO
\end{code}
