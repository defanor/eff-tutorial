\section{Dependent Effects}

\label{sect:depeff}

In the programs we have seen so far, the available effects have remained
constant. Sometimes, however, an operation can \emph{change} the available
effects. The simplest example occurs when we have a state with a dependent
type---adding an element to a vector also changes its type, for example,
since its length is explicit in the type. In this section, we will see how
the \effects{} library supports this. Firstly, we will see how states with
dependent types can be implemented. Secondly, we will see how the effects
can depend on the \emph{result} of an effectful operation. Finally, we will
see how this can be used to implement a type-safe and resource-safe protocol
for file management.

\subsection{Dependent States}

Suppose we have a function which reads input from the console, converts it
to an integer, and adds it to a list which is stored in a \texttt{STATE}.
It might look something like the following:

\begin{code}
readInt : { [STATE (List Int), STDIO] } Eff ()
readInt = do let x = trim !getStr
             put (cast x :: !get)
\end{code}

\noindent
But what if, instead of a list of integers, we would like to store a
\texttt{Vect}, maintaining the length in the type?

\begin{code}
readInt : { [STATE (Vect n Int), STDIO] } Eff ()
readInt = do let x = trim !getStr
             put (cast x :: !get)
\end{code}

\noindent
This will not type check! Although the vector has length \texttt{n} on
entry to \texttt{readInt}, it has length \texttt{S n} on exit. The
\effects{} library allows us to express this as follows:

\begin{code}
readInt : { [STATE (Vect n Int), STDIO] ==>
            [STATE (Vect (S n) Int), STDIO] } Eff ()
readInt = do let x = trim !getStr
             putM (cast x :: !get)
\end{code}

\noindent
The notation \texttt{\{ xs ==> xs' \} Eff a} in a type means that the
operation begins with effects \texttt{xs} available, and ends with effects
\texttt{xs'} available. We have used \texttt{putM} to update the state, where
the \texttt{M} suffix indicates that the \emph{type} is being updated as well
as the value. It has the following type:

\begin{code}
putM : y -> { [STATE x] ==> [STATE y] } Eff () 
\end{code}

\subsection{Result-dependent Effects}

Often, whether a state is updated could depend on the success or otherwise
of an operation. In our \texttt{readInt} example, we might wish to update
the vector only if the input is a valid integer (i.e. all digits). As a
first attempt, we could try the following, returning a \texttt{Bool} which
indicates success:

\begin{code}
readInt : { [STATE (Vect n Int), STDIO] ==>
            [STATE (Vect (S n) Int), STDIO] } Eff Bool
readInt = do let x = trim !getStr
             case all isDigit (unpack x) of
                  False => pure False
                  True => do putM (cast x :: !get)
                             pure True
\end{code}

\noindent
Unfortunately, this will not type check because the vector does not get
extended in both branches of the \texttt{case}!

\begin{code}
MutState.idr:18:19:When elaborating right hand side of Main.case 
block in readInt:
Unifying n and S n would lead to infinite value
\end{code}

\noindent
Clearly, the size of the resulting vector depends on whether or not the
value read from the user was valid. We can express this in the type:

\begin{code}
readInt : { [STATE (Vect n Int), STDIO] ==>
            {ok} if ok then [STATE (Vect (S n) Int), STDIO]
                       else [STATE (Vect n Int), STDIO] } Eff Bool
readInt = do let x = trim !getStr
             case all isDigit (unpack x) of
                  False => pure False 
                  True => do putM (cast x :: !get)
                             pure True 
\end{code}

\noindent
The notation \texttt{\{ xs ==> {res} xs' \} Eff a} in a type means that the
effects available are updated from \texttt{xs} to \texttt{xs'}, \emph{and}
the resulting effects \texttt{xs'} may depend on the result of the operation
\texttt{res}, of type \texttt{a}. Here, the resulting effects are computed
from the result \texttt{ok}---if \texttt{True}, the vector is extended, otherwise
it remains the same. 

When using the function, we will naturally have to check its return value
in order to know what the new set of effects is. For example, to read a set
number of values into a vector, we could write the following:

\begin{code}
readN : (n : Nat) ->
        { [STATE (Vect m Int), STDIO] ==>
          [STATE (Vect (n + m) Int), STDIO] } Eff ()
readN Z = pure ()
readN {m} (S k) = case !readInt of
                      True => rewrite plusSuccRightSucc k m in readN k
                      False => readN (S k)
\end{code}

\noindent
The \texttt{case} analysis on the result of \texttt{readInt} means that
we know in each branch whether reading the integer succeeded, and therefore
how many values still need to be read into the vector. What this means in
practice is that the type system has verified that a necessary dynamic
check (i.e. whether reading a value succeeded) has indeed been done.

\textbf{Aside:} Only \texttt{case} will work here. We cannot use
\texttt{if/then/else} because the \texttt{then} and \texttt{else} branches must
have the same type. The \texttt{case} construct, however,
abstracts over the value being inspected in the type of each branch.

\subsection{File Management}

A practical use for dependent effects is in specifying resource usage protocols
and verifying that they are executed correctly. For example, file management
follows a resource usage protocol with the following (informally specified)
requirements:

\begin{itemize}
\item It is necessary to open a file for reading before reading it
\item Opening may fail, so the programmer should check whether opening 
was successful
\item A file which is open for reading must not be written to, and vice versa
\item When finished, an open file handle should be closed
\item When a file is closed, its handle should no longer be used
\end{itemize}

These requirements can be expressed formally in \effects{}, by creating
a \texttt{FILE\_IO} effect parameterised over a file handle state, which
is either empty, open for reading, or open for writing. The \texttt{FILE\_IO}
effect's definition is given in Listing \ref{eff:fileio}. Note that this
effect is mainly for illustrative purposes---typically we would also like to
support random access files and better reporting of error conditions.

\begin{code}[float=h,frame=single, caption={File I/O Effect}, label=eff:fileio]
module Effect.File

FILE_IO : Type -> EFFECT

data OpenFile : Mode -> Type

open  : String -> (m : Mode) -> 
        { [FILE_IO ()] ==> 
          {ok} [FILE_IO (if ok then OpenFile m else ())] } Eff Bool
close : { [FILE_IO (OpenFile m)] ==> [FILE_IO ()] } Eff ()

readLine  : { [FILE_IO (OpenFile Read)] } Eff String 
writeLine : { [FILE_IO (OpenFile Write)] } Eff ()
eof       : { [FILE_IO (OpenFile Read)] } Eff Bool 

instance Handler FileIO IO
\end{code}

\noindent
In particular, consider the type of \texttt{open}:

\begin{code}
open  : String -> (m : Mode) -> 
        { [FILE_IO ()] ==> 
          {ok} [FILE_IO (if ok then OpenFile m else ())] } Eff Bool
\end{code}

\noindent
This returns a \texttt{Bool} which indicates whether opening the file was
successful. The resulting state depends on whether the operation was successful;
if so, we have a file handle open for the stated purpose, and if not, we have
no file handle. By \texttt{case} analysis on the result, we continue the
protocol accordingly.

\begin{code}[float=h,frame=single, caption={Reading a File}, label=eff:readfile]
readFile : { [FILE_IO (OpenFile Read)] } Eff (List String)
readFile = readAcc [] where
    readAcc : List String -> { [FILE_IO (OpenFile Read)] }
              Eff (List String)
    readAcc acc = if (not !eof)
                     then readAcc (!readLine :: acc)
                     else pure (reverse acc)
\end{code}

Given a function \texttt{readFile} (Listing \ref{eff:readfile})
which reads from an open file until reaching the end, we can write a program
which opens a file, reads it, then displays the contents and closes it,
as follows, correctly following the protocol:

\begin{code}
dumpFile : String -> { [FILE_IO (), STDIO] } Eff ()
dumpFile name = case !(open name Read) of
                    True => do putStrLn (show !readFile)
                               close
                    False => putStrLn ("Error!")
\end{code}

\noindent
The type of \texttt{dumpFile}, with \texttt{FILE\_IO ()} in its effect
list, indicates that any use of the file resource will follow the protocol
correctly (i.e. it both begins and ends with an empty resource). If we fail
to follow the protocol correctly (perhaps by forgetting to close the file,
failing to check that \texttt{open} succeeded, or opening the file for writing)
then we will get a compile-time error. For example, changing \texttt{open name Read}
to \texttt{open name Write} yields a compile-time error of the following
form:

\begin{code}
FileTest.idr:16:18:When elaborating right hand side of Main.case 
block in testFile:
Can't solve goal 
        SubList [(FILE_IO (OpenFile Read))]
                [(FILE_IO (OpenFile Write)), STDIO]
\end{code}

\noindent
In other words: when reading a file, we need a file which is open for reading,
but the effect list contains a \texttt{FILE\_IO} effect carrying a file open
for writing.

\subsection{Pattern-matching bind}

It might seem that having to test each potentially failing operation with
a \texttt{case} clause could lead to ugly code, with lots of nested case
blocks. Many languages support exceptions to improve this, but unfortunately
exceptions may not allow completely clean resource management---for example,
guaranteeing that any \texttt{open} which did succeed has a corresponding close.

Idris supports \emph{pattern-matching} bindings, such as the following:

\begin{code}
dumpFile : String -> { [FILE_IO (), STDIO] } Eff ()
dumpFile name = do True <- open name Read 
                   putStrLn (show !readFile)
                   close
\end{code}

\noindent
This also has a problem: we are no longer dealing with the case where
opening a file failed! The \Idris{} solution is to extend the pattern-matching
binding syntax to give brief clauses for failing matches. Here, for example,
we could write:

\begin{code}
dumpFile : String -> { [FILE_IO (), STDIO] } Eff ()
dumpFile name  = do True <- open name Read | False => putStrLn "Error"
                    putStrLn (show !readFile)
                    close
\end{code}

\noindent
This is exactly equivalent to the definition with the explicit \texttt{case}.
In general, in a \texttt{do}-block, the syntax\ldots

\begin{code}
do pat <- val | <alternatives>
   p
\end{code}

\ldots is desugared to\ldots

\begin{code}
do x <- val
   case x of
        pat => p
        <alternatives>
\end{code}

\noindent
There can be several \texttt{alternatives}, separated by a vertical bar
\texttt{|}. For example, there is a \texttt{SYSTEM} effect which supports
reading command line arguments, among other things (see Appendix
\ref{sect:appendix}). To read command
line arguments, we can use \texttt{getArgs}:

\begin{code}
getArgs : { [SYSTEM] } Eff (List String)
\end{code}

\noindent
A main program can read command line arguments as follows, where in the
list which is returned, the first element \texttt{prog} is the executable name
and the second is an expected argument:

\begin{code}
emain : { [SYSTEM, STDIO] } Eff ()
emain = do [prog, arg] <- getArgs 
           putStrLn $ "Argument is " ++ arg
           {- ... rest of function ... -}
\end{code}

\noindent
Unfortunately, this will not fail gracefully if no argument is given, or
if too many arguments are given. We can use pattern matching bind alternatives
to give a better (more informative) error:

\begin{code}
emain : { [SYSTEM, STDIO] } Eff ()
emain = do [prog, arg] <- getArgs | [] => putStrLn "Can't happen!"
                                  | [prog] => putStrLn "No arguments!"
                                  | _ => putStrLn "Too many arguments!"
           putStrLn $ "Argument is " ++ arg
           {- ... rest of function ... -}
\end{code}

\noindent
If \texttt{getArgs} does not return something of the form \texttt{[prog, arg]}
the alternative which does match is executed instead, and that value returned.

